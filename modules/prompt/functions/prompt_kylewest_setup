#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Kyle West <kswest@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Indicates dirty Git repository.
#   - Indicates the Ruby version.
#   - Indicates vi modes.
#
# Screenshots:
#   http://i.imgur.com/dCwhynn.png
#

CROSS="✘"

function ahead_behind {
	local curr_branch=$(git rev-parse --abbrev-ref HEAD) &> /dev/null;
	local curr_remote=$(git config branch.$curr_branch.remote) &> /dev/null;
	if [[ "$curr_remote" = "" ]]; then
		return 0
	fi
	local curr_merge_branch=$(git config branch.$curr_branch.merge | cut -d / -f 3) &> /dev/null;
	local ahead_behind=$(git rev-list --left-right --count $curr_remote/$curr_merge_branch...$curr_branch)
	local ahead=$(echo $ahead_behind | cut -f2)
	local behind=$(echo $ahead_behind | cut -f1)
	if [[ $ahead == 0 ]]; then
		if [[ $behind != 0 ]]; then
			echo "%{%F{red}%}$behind "
		fi
	elif [[ $behind == 0 ]]; then
		if [[ $ahead != 0 ]]; then
			echo "%{%F{green}%}$ahead "
		fi
	else
		echo "%{%F{green}%}$ahead%{%F{reset}%}|%{%F{red}%}$behind "
	fi
}

function behind {
	local curr_branch=$1
	local curr_remote=$2
	local curr_merge_branch=$3
	git rev-list --count $curr_branch...$curr_remote/$curr_merge_branch;
}

function ahead {
	local curr_branch=$1
	local curr_remote=$2
	local curr_merge_branch=$3
	git rev-list --left-right --count $curr_remote/$curr_merge_branch...$curr_branch | cut -f2;
}

function prompt_kylewest_precmd {
  RETVAL=$?
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  SYMBOLS=""
  [[ $RETVAL -ne 0 && $RETVAL -ne 146 ]] && SYMBOLS+="%{%F{red}%}$CROSS "

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_kylewest_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_kylewest_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B%F{green}❯%f%b"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "%B%F{magenta}❮%f%b"

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:branch' format '%F{yellow}%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%B%F{red}!%f%b'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:keys' format 'prompt' '- %b%D '

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{blue}[%v]%f'

  # Define prompts.
  PROMPT='${SYMBOLS}%F{cyan}%2~%f ${git_info:+${(e)git_info[prompt]}}$(ahead_behind)${editor_info[keymap]} '
  RPROMPT='${ruby_info[version]}'
}

prompt_kylewest_setup "$@"
